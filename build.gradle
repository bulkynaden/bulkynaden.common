plugins {
    id 'java'
    id 'java-library'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'maven-publish'
}

group = 'es.bulkynaden'
version = '0.0.1'

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "es.bulkynaden"
            artifactId = "common"
            version = "0.0.1"
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/blkndn/common"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

jar {
    enabled = true
}

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

test {
    useJUnitPlatform()
}